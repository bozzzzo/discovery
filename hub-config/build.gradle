apply plugin: 'maven'
apply plugin: 'signing'

group = project.parent.group
version = project.parent.version

ext {
  assertjVersion = '3.2.0'
  commonsLang3 = '3.4'
  jacksonVersion = '2.6.3'
  junitVersion = '4.12'
  logbackVersion = '1.1.3'
  slf4jVersion = '1.7.13'
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}


dependencies {
  compile "ch.qos.logback:logback-classic:${logbackVersion}"

  compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"

  compile "org.apache.commons:commons-lang3:${commonsLang3}"
  compile "org.slf4j:slf4j-api:${slf4jVersion}"

  testCompile(
      "org.assertj:assertj-core:${assertjVersion}",
      "junit:junit:${junitVersion}"
  )
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'hub-config'
        packaging 'jar'
        description 'Datawire Hub Configuration Parser'
        url 'https://github.com/datawire/hub'

        scm {
          connection 'scm:git:git://github.com/datawire/hub.git'
          developerConnection 'scm:git:git://github.com/datawire/hub.git'
          url 'https://github.com/datawire/hub'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'plombardi'
            name 'Philip Lombardi'
            email 'plombardi@datawire.io'
          }
        }
      }
    }
  }
}